/*
 *
 *       Auteur: Pedrono Marie-Jo
 *
 * 	crÃ©ation : 13 fÃ©vrier 2002
 *
 *     nature : projet etudiant
 *
 */


options{
	JDK_VERSION="1.5";	
}

PARSER_BEGIN(Yaka)



public class Yaka {

	static Expression exp = new Expression();
	static Declaration declaration;
	static TabIdent tabIdent = new TabIdent();
	static YVM yvm;
	static YVMasm yvmAsm;
	
	static int taille = 0;
	static String typeAffect;
	
  public static void main(String args[]) { 
    Yaka analyseur;
    java.io.InputStream input;
	
    if (args.length==1) {
      System.out.print(args[args.length-1] + ": ");
      try {
        input = new java.io.FileInputStream(args[args.length-1]+".yaka");
      } catch (java.io.FileNotFoundException e) {
        System.out.println("Fichier introuvable.");
        return;
      }
    } else if (args.length==0) {
      System.out.println("Lecture sur l'entree standard...");
      input = System.in;
    } else {
      System.out.println("Usage: java Gram [fichier]");
      return;
    }
    try {
      analyseur = new Yaka(input);
      analyseur.analyse();
      System.out.println("analyse syntaxique reussie!");
    } catch (ParseException e) {
      String msg = e.getMessage();
      msg = msg.substring(0,msg.indexOf("\n"));
      System.out.println("Erreur de syntaxe : "+msg);
    }
  } 
}

PARSER_END(Yaka)
/***************************************/
/********** TOKEN DEFINITIONS **********/
/***************************************/

TOKEN_MGR_DECLS :
{public static String identLu,chaineLue;
 public static int entierLu;}

/*** Skip whitespace and comments ***/
SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| "(*"   : IN_COMMENT
}
<IN_COMMENT> MORE:
{
  < ~[] >
}
<IN_COMMENT> SKIP:
{
   < "*)" >  {SwitchTo(DEFAULT);} 
}


/* Mots rÃ©servÃ©s*/

TOKEN :
{
  < BOOLEEN: "BOOLEEN" >
| < VAR: "VAR" >
| < FAIRE: "FAIRE" >
| < SINON: "SINON" >
| < POUR: "POUR" >
| < SI: "SI" >
| < FSI: "FSI">
| < ENTIER: "ENTIER" >
| < RETOURNE: "RETOURNE" >
| < VRAI: "VRAI" >
| < TANTQUE: "TANTQUE" >
| <CONST : "CONST">
| <FAUX : "FAUX">
| <FAIT : "FAIT">
| <OU : "OU">
| <ET : "ET">
| <NON : "NON">
| <FONCTION : "FONCTION">
| <FFONCTION  : "FFONCTION">
| <PROGRAMME : "PROGRAMME" >
| <FPROGRAMME: "FPROGRAMME" >
| <PRINCIPAL : "PRINCIPAL">
| <FPRINCIPAL : "FPRINCIPAL">
| <ALORS : "ALORS">
| <ECRIRE : "ECRIRE">
| <LIRE : "LIRE">
| <ALALIGNE : "ALALIGNE">

}



/*** unites de base nombres, idents,  strings ***/
TOKEN  :
{
  < #chiffre : ["0"-"9"] >
| < entier : (<chiffre>)+ > 
	{ entierLu = Integer.parseInt(image.toString());
        }
| < #lettre: ["A"-"Z","a"-"z"] >
| < ident : <lettre> (<lettre> | <chiffre>)* >
	{identLu =image.toString();	
	}
| < chaine : "\"" (~["\""])* "\"" | "'" (~["'"])* "'" >
	{ chaineLue = image.toString();
	 }
}


/**************************************/
/********debut de la grammaire ********/
/**************************************/
void analyse() : {}
{

   <PROGRAMME> <ident>	{	yvm.entete(YakaTokenManager.identLu);
   							yvmAsm.entete(YakaTokenManager.identLu);}

   bloc()
   <FPROGRAMME> { yvm.queue();
   				  yvmAsm.queue();}
}

void bloc() : {}{
 (declConst())*
 (declVar())*	{
 					yvmAsm.ouvrePrinc(taille);
 					yvm.ouvrePrinc(taille);
 				}	
   suiteInstr() 
}

void declConst() : {}
{
  <CONST>  defConst() ( "," defConst())* ";"
}

void defConst() : {}
{
  <ident> {declaration.setSaveName(YakaTokenManager.identLu);}
   "=" valConst()   

}

void valConst() : {}{

  <entier> 	{tabIdent.rangeIdent(declaration.getSaveName(), declaration.createIdentConst("ENTIER", YakaTokenManager.entierLu));}
 | <ident>	{	if(tabIdent.existeIdent(YakaTokenManager.identLu)){
 					tabIdent.rangeIdent(declaration.getSaveName(), tabIdent.chercheIdent(YakaTokenManager.identLu));
 				}else System.out.println("Erreur : Variable non déclarée");
 			}	
 | <VRAI> 	{tabIdent.rangeIdent(declaration.getSaveName(), declaration.createIdentConst("BOOLEEN", VRAI));}	
 | <FAUX> 	{tabIdent.rangeIdent(declaration.getSaveName(), declaration.createIdentConst("BOOLEEN", FAUX));}	

}


void declVar() : {}
{
  <VAR> type()	
  <ident>		{
  					if(tabIdent.existeIdent(YakaTokenManager.identLu)){
  						System.out.println("Erreur : Variable deja  declaree");
  					}else{
  						tabIdent.rangeIdent(YakaTokenManager.identLu, declaration.createIdentVar(declaration.getSaveName()));
  						taille += 2;
  					}
  				}	
  ("," 
      <ident> 	{
					if(tabIdent.existeIdent(YakaTokenManager.identLu)){
  						System.out.println("Erreur : Variable deja  declaree");
  					}else{
  						tabIdent.rangeIdent(YakaTokenManager.identLu, declaration.createIdentVar(declaration.getSaveName()));
  						taille +=2;
  					}
				}
  )* ";"
	
}

void type() :{}
{

   <ENTIER> 	{declaration.setSaveName("ENTIER");} 		
 | <BOOLEEN>	{declaration.setSaveName("BOOLEEN");}	

}


/*
 * Syntaxe des instructions.
 */

void suiteInstr() : {}
{ 
(instruction() (";" (instruction())? )*) 
}

void instruction() : {}
{
  affectation()
 |lecture()
 |ecriture()	
}

void affectation() : {}
{

  <ident> 	{	declaration.setSaveName(YakaTokenManager.identLu);
  				typeAffect = tabIdent.chercheIdent(declaration.getSaveName()).getType();
  			}
  "=" expression() {
  						if(typeAffect.equals(exp.tipToString(exp.popType()))){
  							yvm.istore(((IdVar)tabIdent.chercheIdent(declaration.getSaveName())).getOffset());
  							yvmAsm.istore(((IdVar)tabIdent.chercheIdent(declaration.getSaveName())).getOffset());
  						}
  						else{
  							System.out.println("erreur de type au niveau de l'affectation");
						}
					}

}
void lecture() : {}
{
  <LIRE> "(" <ident> {
  						yvm.lireEnt(((IdVar)tabIdent.chercheIdent(declaration.getSaveName())).getOffset());
  						yvmAsm.lireEnt(((IdVar)tabIdent.chercheIdent(declaration.getSaveName())).getOffset());
  					  }
  ")" 
}

void ecriture() : {}
{
  (<ECRIRE> "(" expression() {
  								yvm.ecrireEnt();
  								yvmAsm.ecrireEnt();
  							} 
  							
  | <chaine>				{
  								yvm.ecrireChaine(YakaTokenManager.chaineLue);
  								yvmAsm.ecrireChaine(YakaTokenManager.chaineLue);
  							}
  							
   ")" ) | <ALALIGNE>		{
   								yvm.aLaLigne();
   								yvmAsm.aLaLigne();
   							}
}



/*
 * Expression .
 */
void expression() : {}
{
  simpleExpr() 
  (opRel()
  simpleExpr() {
  				Expression.op o = exp.peekOpera();
	   			switch(o){
	   				case EGAL :
	   					yvm.iegal();
	   					yvmAsm.iegal();
	   					break;
	   				case DIFF :
	   					yvm.idiff();
	   					yvmAsm.idiff();
	   					break;
	   				case INF :
	   					yvm.iinf();
	   					yvmAsm.iinf();
	   					break;
	   				case INFEGAL :
	   					yvm.iinfegal();
	   					yvmAsm.iinfegal();
	   					break;
	   				case SUP :
	   					yvm.isup();
	   					yvmAsm.isup();
	   					break;
	   				case SUPEGAL :
	   					yvm.isupegal();
	   					yvmAsm.isupegal();
	   					break;
	   				default :
	   			}
  				exp.testType2Argv();
  				}

  )?
}



void simpleExpr() : {}
{
   terme() 
   (opAdd()
   terme() {
   			Expression.op o = exp.peekOpera();
   			switch(o){
   				case PLUS :
   					yvm.iadd();
   					yvmAsm.iadd();
   					break;
   				case MOINS :
   					yvm.isub();
   					yvmAsm.isub();
   					break;
   				case OU :
   					yvm.ior();
   					yvmAsm.ior();
   					break;
   				default :
   			}
   			exp.testType2Argv();
   			}

   )*
} 
void terme() : {}
{
  facteur() 
  (opMul()
  facteur()	{
  			Expression.op o = exp.peekOpera();
   			switch(o){
   				case MUL :
   					yvm.imul();
   					yvmAsm.imul();
   					break;
   				case DIV :
   					yvm.idiv();
   					yvmAsm.idiv();
   					break;
   				case ET :
   					yvm.iand();
   					yvmAsm.iand();
   					break;
   				default :
   			}
  			exp.testType2Argv();
  			}

  )*
}
void facteur() : {}
{   primaire() 
  |  opNeg() primaire() {
  						Expression.op o = exp.peekOpera();
			   			switch(o){
			   				case NON :
			   					yvm.inot();
			   					yvmAsm.inot();
			   					break;
			   				case MOINS :
			   					yvm.ineg();
			   					yvmAsm.ineg();
			   					break;
			   				default :
			   			}
  						exp.testType1Argv();
  						} 
}

void primaire (): {}
{
   valeur()
 | "(" expression() ")" 
 
}

void valeur () : {}

{ <entier> {exp.saveType(Expression.tip.ENTIER);} 		
 | <ident> 	{exp.saveType(exp.stringTotip(TabIdent.chercheIdent(YakaTokenManager.identLu).getType()));}	
 | <VRAI> 	{exp.saveType(Expression.tip.BOOL);} 
 | <FAUX> 	{exp.saveType(Expression.tip.BOOL);} 

 
}

void opRel() : {}
{

   "=" 		{exp.saveOp(Expression.op.EGAL);}
 | "<>" 	{exp.saveOp(Expression.op.DIFF);}
 | "<" 		{exp.saveOp(Expression.op.INF);}
 | "<=" 	{exp.saveOp(Expression.op.INFEGAL);}
 | ">" 		{exp.saveOp(Expression.op.SUP);}
 | ">=" 	{exp.saveOp(Expression.op.SUPEGAL);}

} 

void opAdd() : {}
{

    "+"	 {exp.saveOp(Expression.op.PLUS);}	
  |  "-" {exp.saveOp(Expression.op.MOINS);}	
  | <OU> {exp.saveOp(Expression.op.OU);}	

}

void opMul() : {}
{

  "*"	{exp.saveOp(Expression.op.MUL);}	
  | "/"	{exp.saveOp(Expression.op.DIV);}	
  | <ET> {exp.saveOp(Expression.op.ET);}	

} 

void opNeg() : {}
{ 
 "-" 		{exp.saveOp(Expression.op.MOINS);}	
 | <NON>	{exp.saveOp(Expression.op.NON);}
}


